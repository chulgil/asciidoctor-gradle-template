plugins {
    id 'org.asciidoctor.jvm.convert' version '3.1.0'
    id 'org.asciidoctor.jvm.pdf' version '3.1.0'
    id 'org.asciidoctor.jvm.gems' version '3.1.0'
}

repositories {
    mavenCentral()
    ruby {
        gems()
    }
}

dependencies {
    asciidoctorGems files('repos/gem/prawn-svg-font-patch-0.1.2.gem')
    asciidoctorGems 'rubygems:asciidoctor-pdf-linewrap-ja:0.6.0'
}

asciidoctor {
    dependsOn asciidoctorGemsPrepare
    baseDir file('src/docs/asciidoc')
    sourceDir file('src/docs/asciidoc')
    sources {
        include 'index.adoc'
    }
    resources {
        from('src/docs/asciidoc') {
            include 'Chapter*/images/*'
        }
    }
    asciidoctorj {
        attributes 'build-gradle': file('build.gradle'),
            'sourcedir': 'src/',
            'outdir': "./",
            'source-highlighter': 'coderay',
            'stylesdir': '@style',
            'stylesheet': 'asciidoctor.css'
    }
}

asciidoctorPdf {
    dependsOn asciidoctorGemsPrepare
    baseDir file('src/docs/asciidoc')
    sourceDir file('src/docs/asciidoc')
    fontsDir file('src/docs/asciidoc/@font')
    sources {
        include 'index.adoc'
    }
    asciidoctorj {
        attributes 'build-gradle': file('build.gradle'),
            'sourcedir': 'src/',
            'outdir': "./",
            'source-highlighter': 'coderay',
            'pdf-stylesdir': "@style",
            'pdf-style': 'pdf'
    }
}

asciidoctorj {
    modules {
        diagram.use()
        diagram.version '1.5.16'
        pdf.version '1.5.3'
    }
    requires = [
        'asciidoctor-diagram',
        'asciidoctor-pdf-linewrap-ja',
        'prawn-svg-font-patch'
    ]
}
